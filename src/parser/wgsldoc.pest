SHADER = _{ SOI ~ GLOBAL_DOCS? ~ (IMPORT | BUILTIN_IMPORT | _PUSH_CONSTANTS | FUNCTION | STRUCTURE | RESOURCE_BINDING | CONST)* ~ EOI }

// BINDINGS

NUMBER = { ASCII_DIGIT+ }

ATTR_GROUP   = { "@group(" ~ NUMBER ~ ")" }
ATTR_BINDING = { "@binding(" ~ NUMBER ~ ")" }

BINDING_ATTRS = { ATTR_GROUP ~ ATTR_BINDING }

STORAGE_CLASS = { "uniform" | "storage" | "private" | "workgroup" }
VAR_TEMPLATE  = { "<" ~ STORAGE_CLASS ~ ">" }

RESOURCE_BINDING = {
  DOCS? ~ BINDING_ATTRS ~
  "var" ~ (VAR_TEMPLATE)? ~ IDENT ~ ":" ~ TYPE ~ ";"?
}

// CONSTANTS

CONST = { DOCS? ~ "const" ~ IDENT ~ (":" ~ TYPE)? ~ "=" ~ CONST_VALUE ~ ";" }
CONST_VALUE = { (!";" ~ ANY)* }

// DECORATORS

LOCATION = _{ "@location(" ~ SILENT_NUMBER ~ ")" }

SILENT_NUMBER = _{ ASCII_DIGIT+ }

ENTRY = _{ "@fragment" | "@vertex" | "@compute" }

// PUSH_CONSTANTS

_PUSH_CONSTANTS = { "var<push_constant>" ~ IDENT ~ ":" ~ TYPE ~ ";" }

// IMPORTS

IMPORT_PATH = { PATH }

MODULE_NAME = { IDENT }

IMPORT = { DOCS? ~ "#import" ~ IMPORT_PATH ~ "as" ~ MODULE_NAME ~ ";"? }

BUILTIN_IMPORT = { DOCS? ~ "#import" ~ BUILTIN_IMPORT_CONTENT ~ ";"? }

BUILTIN_IMPORT_CONTENT = { IMPORT_PATH ~ ("::" ~ (MODULE_NAME | IMPORT_LIST))* }

IMPORT_LIST = { "{" ~ MODULE_NAME ~ ("," ~ MODULE_NAME)* ~ ","? ~"}" }

// STRUCTURES

STRUCTURE = { DOCS? ~ "struct" ~ IDENT ~ "{" ~ FIELDS ~ "}" }

FIELDS = { (FIELD ~ ",")* ~ FIELD? }

FIELD = { DOCS? ~ IDENT ~ ":" ~ LOCATION? ~ TYPE }

// FUNCTIONS

FUNCTION = { DOCS? ~ ENTRY? ~ "fn" ~ IDENT ~ "(" ~ ARGS? ~ ")" ~ RETURN? ~ CODE_BLOCK ~ ";"? }

ARGS = { (ARG ~ ",")* ~ ARG? }

ARG = { DOCS? ~ IDENT ~ ":" ~ FUNCTION_TYPE }

RETURN = { "->" ~ LOCATION? ~ TYPE }

CODE_BLOCK  = _{ "{" ~ (CODE_CONTENT)* ~ "}" }
CODE_CONTENT = _{ (!("{" | "}")) ~ ANY | CODE_BLOCK }

// TYPES

TYPE = { PRIMITIVE | VECTOR | PATH_TYPE }
FUNCTION_TYPE = { FUNCTION_POINTER | PRIMITIVE | VECTOR | PATH_TYPE }

FUNCTION_POINTER = { "ptr" ~ "<" ~ "function" ~ "," ~ TYPE ~ ">" }

GENERIC_ARGS = { "<" ~ TYPE ~ ("," ~ TYPE)* ~ ">" }

PATH_TYPE = { (MODULE ~ "::")? ~ IDENT ~ GENERIC_ARGS? }

MODULE = { IDENT }

PRIMITIVE = { ("f" ~ ("32"|"64")) | (("i"|"u") ~ ("8"|"16"|"32"|"64")) | "bool" }

VECTOR = { "vec" ~ VECTOR_DIMENSION ~ "<" ~ PRIMITIVE ~ ">" }

VECTOR_DIMENSION = { "2"|"3"|"4" }

// GENERIC

IDENT = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

PATH = @{ (ASCII_ALPHANUMERIC|"/"|"."|"_")+ }

// DOCS

GLOBAL_DOCS = { ("//!" ~ DOCS_CONTENT)+ }

DOCS = { ("///" ~ DOCS_CONTENT)+ }

DOCS_CONTENT = @{ (!NEWLINE ~ ANY)* }

// BUILTIN

COMMENT = _{ "//" ~ !("/"|"!") ~ (!NEWLINE ~ ANY)* }

WHITESPACE = _{ " " | "\t" | "\n" | "\r\n" }
